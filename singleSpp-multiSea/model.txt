model
{
    psi1 ~ dunif(0, 1)
    for (k in 1:(nyear - 1)) {
        phi[k] ~ dunif(0, 1)
        gamma[k] ~ dunif(0, 1)
        p[k] ~ dunif(0, 1)
    }
    p[nyear] ~ dunif(0, 1)
    for (i in 1:nsite) {
        z[i, 1] ~ dbern(psi1)
        for (k in 2:nyear) {
            muZ[i, k] <- z[i, k - 1] * phi[k - 1] + (1 - z[i, k - 1]) * gamma[k - 1]
            z[i, k] ~ dbern(muZ[i, k])
        }
    }
    for (i in 1:nsite) {
        for (k in 1:nyear) {
            muy[i, k] <- z[i, k] * p[k]
            y[i, 1:nrep, k] ~ dbern_vec(muy[i, k], nrep)
        }
    }
    psi[1] <- psi1
    n.occ[1] <- sum(z[1:nsite, 1])
    for (k in 2:nyear) {
        psi[k] <- psi[k - 1] * phi[k - 1] + (1 - psi[k - 1]) * gamma[k - 1]
        n.occ[k] <- sum(z[1:nsite, k])
        growthr[k - 1] <- psi[k]/psi[k - 1]
        turnover[k - 1] <- (1 - psi[k - 1]) * gamma[k - 1]/psi[k]
    }
}
