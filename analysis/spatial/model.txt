model
{
    delta ~ dunif(0.1, 10)
    sigma ~ dunif(0.1, 10)
    p ~ dunif(0, 1)
    alpha ~ dnorm(0, 0.001)
    b1 ~ dnorm(0, 0.001)
    rho[1:nsite] ~ dmnorm(zeros[1:nsite], D.tau[1:nsite, 1:nsite])
    for (i in 1:nsite) {
        z[i] ~ dbern(psi[i])
        logit(psi[i]) <- alpha + b1 * elev[i] + rho[i]
        p.eff[i] <- z[i] * p
        for (j in 1:nreps) {
            y[i, j] ~ dbern(p.eff[i])
        }
    }
    for (i in 1:nsite) {
        for (j in 1:nsite) {
            prep.cov[i, j] <- exp(-delta * D[i, j])
            D.cov[i, j] <- (sigma^2) * (0.95 * prep.cov[i, j] + 0.05 * DI[i, j])
        }
    }
    D.tau[1:nsite, 1:nsite] <- inverse(D.cov[1:nsite, 1:nsite])
}
